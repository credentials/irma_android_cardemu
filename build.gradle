apply plugin: 'com.android.application'
apply plugin: 'maven'

group="org.irmacard"

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
    }
}

repositories {
    mavenLocal()
    maven {
        url "https://credentials.github.io/repos/maven2/"
    }

    // Use this to get minimal android library
    maven {
        url "https://raw.github.com/embarkmobile/zxing-android-minimal/mvn-repo/maven-repository/"
    }
    mavenCentral()
    jcenter()
}

android {
    compileSdkVersion 23
    buildToolsVersion '26.0.2'

    lintOptions {
        // BCProv references javax.naming
        // CredentialsAPI references java.awt and java.swing
        disable 'InvalidPackage'
    }

    buildTypes.all { type ->
        // ACRA configuration, disabled by default. Override in acra.cradle
        type.buildConfigField "String", "acraServer", '""'
        type.buildConfigField "String", "acraLogin", '""'
        type.buildConfigField "String", "acraPassword", '""'
        type.buildConfigField "org.acra.ReportingInteractionMode", "acraMode",
                "org.acra.ReportingInteractionMode.SILENT"
        type.resValue "string", "crash_toast_text", ""
    }

    packagingOptions {
        pickFirst  'META-INF/LICENSE'
    }
}

if (project.file('acra.gradle').exists()) { apply from: 'acra.gradle' }
if (project.file('signingkeys.gradle').exists()) { apply from: 'signingkeys.gradle' }


dependencies {
    // Required -- JUnit 4 framework
    testCompile 'junit:junit:4.12'

    // Google stuff
    compile 'com.android.support:appcompat-v7:23+'
    compile 'com.android.support:support-annotations:23+'
    compile 'com.google.code.gson:gson:2.2.2'
    compile('com.google.http-client:google-http-client-android:1.20.0') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    // zxing QR code libraries
    compile 'com.journeyapps:zxing-android-embedded:3.3.0@aar'
    compile 'com.google.zxing:core:3.2.1'

    // Other stuff
    compile 'ch.acra:acra:4.6.2'
    compile 'com.noveogroup.android:android-logger:1.3.5'
    compile 'com.github.nicolasjafelle:paginglistview:1.2'

    // Our stuff
    compile('org.irmacard.credentials:credentials_api:0.18.1') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile('org.irmacard.credentials:credentials_idemix:0.17.3') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile('org.irmacard.api:irma_api_common:0.7.8') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile('org.irmacard.keyshare:irma_keyshare_common:0.0.4') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}
