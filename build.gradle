apply plugin: 'com.android.application'
apply plugin: 'maven'

version="2.1"
group="org.irmacard"

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
    }
}

repositories {
    mavenLocal()
    maven {
        url "https://credentials.github.io/repos/maven2/"
    }

    // Use this to get minimal android library
    maven {
        url "https://raw.github.com/embarkmobile/zxing-android-minimal/mvn-repo/maven-repository/"
    }
    mavenCentral()
    jcenter()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    dexOptions {
        incremental true
    }

    lintOptions {
        // BCProv references javax.naming
        // CredentialsAPI references java.awt and java.swing
        disable 'InvalidPackage'
    }

    buildTypes.all { type ->
        // ACRA configuration, disabled by default. Override in acra.cradle
        type.buildConfigField "String", "acraServer", '""'
        type.buildConfigField "String", "acraLogin", '""'
        type.buildConfigField "String", "acraPassword", '""'
        type.buildConfigField "org.acra.ReportingInteractionMode", "acraMode",
                "org.acra.ReportingInteractionMode.SILENT"
        type.resValue "string", "crash_toast_text", ""

        // Self enroll server
        type.buildConfigField "String", "enrollServer", '"https://demo.irmacard.org/tomcat/irma_mno_server/api"'
    }

    // Enable metrics only in release builds
    buildTypes {
        release {
            buildConfigField "String", "metricServer", '"https://demo.irmacard.org/tomcat/irma_metrics_server/api/v1"'
        }
        debug {
            buildConfigField "String", "metricServer", '""'
        }
    }
}

if (project.file('acra.gradle').exists()) { apply from: 'acra.gradle' }
if (project.file('enroll.gradle').exists()) { apply from: 'enroll.gradle' }
if (project.file('signingkeys.gradle').exists()) { apply from: 'signingkeys.gradle' }


dependencies {
    // Google stuff
    compile 'com.android.support:appcompat-v7:23+'
    compile 'com.android.support:support-annotations:23+'
    compile 'com.google.code.gson:gson:2.2.2'

    // zxing QR code libraries
    compile 'com.journeyapps:zxing-android-embedded:3.3.0@aar'
    compile 'com.google.zxing:core:3.2.1'

    // Other stuff
    compile 'ch.acra:acra:4.6.2'
    compile 'com.noveogroup.android:android-logger:1.3.5'
    compile 'com.madgag.spongycastle:prov:1.52.0.0'

    // Android-Scuba bridge. Scuba itself is loaded by JMRTD, which is loaded by irma_mno_common.
    compile 'net.sf.scuba:scuba-sc-android:0.0.10'

    // Our stuff
    compile "org.irmacard.android:irma_android_library:0.13.9"
    compile 'org.irmacard.metrics:irma_metrics_common:0.1.1'
    compile 'org.irmacard.mno:irma_mno_common:0.2.1'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
